;;; -*- lexical-binding: t -*-
#+PROPERTY: header-args:emacs-lisp :tangle yes :lexical t
#+TITLE: Emacs Configuration
#+AUTHOR: Femi Agbabiaka
#+STARTUP: overview

* Introduction
This is a literate Emacs configuration optimized for fast startup and efficient lazy loading.
The configuration is organized into logical sections with performance as a primary concern.

* Bootstrap & Performance
** Custom File Location
Emacs custom.el is notoriously messy. Keep it separate from init.el.
#+begin_src emacs-lisp
  (setq custom-file (concat user-emacs-directory "custom.el"))
  (load custom-file 'noerror)
#+end_src

** Garbage Collection Strategy
Maximize GC threshold during startup, then use GCMH for intelligent GC during idle.
Also set process output max for better LSP performance.
#+begin_src emacs-lisp
  ;; Maximize GC threshold during startup; remains at maximum until after initialization,
  ;; when gcmh-mode is activated (via after-init-hook) and restores it to a reasonable value (16MB).
  (setq gc-cons-threshold most-positive-fixnum)

  ;; Log startup time
  (add-hook 'emacs-startup-hook
            (lambda ()
              (message "*** Emacs loaded in %s with %d garbage collections."
                       (format "%.2f seconds"
                               (float-time
                                (time-subtract after-init-time before-init-time)))
                       gcs-done)))

  ;; Increase process output for LSP performance
  (setq read-process-output-max (* 1024 1024))  ; 1MB
#+end_src

** Startup Profiling
Benchmark startup time and identify slow packages.
Only loads when EMACS_BENCHMARK env var is set.
#+begin_src emacs-lisp
  (use-package benchmark-init
    :if (getenv "EMACS_BENCHMARK")
    :config
    (add-hook 'after-init-hook 'benchmark-init/deactivate))
#+end_src

** File Management
Configure backup, auto-save, and lock file locations to avoid clutter.
#+begin_src emacs-lisp
  (setq lock-file-name-transforms
        '(("\\`/.*/\\([^/]+\\)\\'" "~/.emacs.d/.cache/\\1" t)))
  (setq auto-save-file-name-transforms
        '(("\\`/.*/\\([^/]+\\)\\'" "~/.emacs.d/.cache/\\1" t)))
  (setq backup-directory-alist
        '((".*" . "~/.emacs.d/.cache/")))
  (setq backup-inhibited t)
  (setq auto-save-default nil)
#+end_src

** Exit Confirmation
Make it harder to accidentally kill Emacs (except in daemon mode).
#+begin_src emacs-lisp
  (unless (daemonp)
    (setq confirm-kill-emacs 'y-or-n-p))
#+end_src

* Core Emacs Settings
** Built-in Emacs Configuration
Configure core Emacs behavior and enable tree-sitter modes.
#+begin_src emacs-lisp
  (use-package emacs
    :custom
    ;; Enable context menu for mouse right-click
    (context-menu-mode t)
    ;; Allow recursive minibuffer editing
    (enable-recursive-minibuffers t)
    ;; Hide commands that don't work in current mode
    (read-extended-command-predicate #'command-completion-default-include-p)
    ;; Prevent cursor from entering minibuffer prompt
    (minibuffer-prompt-properties
     '(read-only t cursor-intangible t face minibuffer-prompt))
    ;; Tab key behavior: indent then complete
    (tab-always-indent 'complete)
    ;; Disable Ispell completion in text-mode (use cape-dict instead)
    (text-mode-ispell-word-completion nil)
    ;; Use tree-sitter modes by default
    (major-mode-remap-alist
     '(;; Languages with explicit tree-sitter support
       (nix-mode . nix-ts-mode)
       (rust-mode . rust-ts-mode)
       (go-mode . go-ts-mode)
       (yaml-mode . yaml-ts-mode)
       (ruby-mode . ruby-ts-mode)
       (dockerfile-mode . dockerfile-ts-mode)
       (typescript-mode . typescript-ts-mode)
       (zig-mode . zig-ts-mode)
       ;; Standard formats
       (js-mode . js-ts-mode)
       (json-mode . json-ts-mode)
       (css-mode . css-ts-mode)
       (sh-mode . bash-ts-mode)
       (conf-toml-mode . toml-ts-mode)
       ;; C/C++
       (c-mode . c-ts-mode)
       (c++-mode . c++-ts-mode)
       (c-or-c++-mode . c-or-c++-ts-mode))))
#+end_src

** Environment Variables
Load PATH and other environment variables from shell (macOS/GUI only).
Deferred to window setup to avoid blocking startup.
#+begin_src emacs-lisp
  (use-package exec-path-from-shell
    :if (memq window-system '(mac ns x))
    :hook (window-setup-hook . exec-path-from-shell-initialize)
    :custom
    ;; Use bash for speed (faster than fish for this purpose)
    (exec-path-from-shell-shell-name "bash")
    (exec-path-from-shell-variables
     '("PATH" "MANPATH" "NIX_PATH" "NIX_SSL_CERT_FILE")))
#+end_src

** macOS-Specific Settings
Configure modifier keys for macOS.
#+begin_src emacs-lisp
  (when (eq system-type 'darwin)
    (setq mac-option-modifier 'meta)    ; Option = Meta
    (setq mac-command-modifier 'super)) ; Command = Super
#+end_src

** Text Editing Defaults
#+begin_src emacs-lisp
  ;; Indentation and tabs
  (setq-default tab-width 2)
  (setq-default default-tab-width 2)
  (setq-default indent-tabs-mode nil)

  ;; Scrolling behavior
  (setq scroll-conservatively scroll-margin)
  (setq scroll-step 1)
  (setq scroll-preserve-screen-position t)
  (setq scroll-error-top-bottom t)
  (setq mouse-wheel-progressive-speed nil)
  (setq mouse-wheel-inhibit-click-time nil)

  ;; Clipboard integration
  (setq select-enable-clipboard t)
  (setq mouse-yank-at-point t)

  ;; Enable automatic delimiter pairing (deferred)
  (add-hook 'after-init-hook #'electric-pair-mode)
#+end_src

** Delete Trailing Whitespace
Remove trailing whitespace on save (except in markdown).
#+begin_src emacs-lisp
  (defvar my/trailing-whitespace-modes '(markdown-mode)
    "List of modes where trailing whitespace should NOT be deleted.")

  (add-hook 'before-save-hook
            (lambda ()
              (unless (seq-some #'derived-mode-p my/trailing-whitespace-modes)
                (delete-trailing-whitespace))))
#+end_src

** File History
Remember recently edited files and cursor positions.
Deferred to avoid loading tramp at startup.
#+begin_src emacs-lisp
  (use-package recentf
    :ensure nil
    :hook (after-init-hook . recentf-mode)
    :custom
    (recentf-auto-cleanup 'never)
    ;; Set excludes before mode activates to avoid tramp autoload
    (recentf-exclude '("\\`/\\w+:" "/ssh:" "/sudo:" "/docker:")))

  (use-package saveplace
    :ensure nil
    :hook (after-init-hook . save-place-mode)
    :custom
    (save-place-ignore-files-regexp
     "\\`/\\(?:ssh\\|scp\\|ftp\\|sudo\\|docker\\):"))
#+end_src

** Dired Configuration
Enhanced directory editing with dired-x. Deferred until dired is actually used.
#+begin_src emacs-lisp
  (use-package dired
    :ensure nil
    :defer t
    :custom
    (dired-dwim-target t)
    (dired-listing-switches "-lah"))

  (use-package dired-x
    :ensure nil
    :after dired
    :hook (dired-mode . dired-extra-startup))
#+end_src

** Terminal Only Settings
We can take advantage of kitty's terminal protocol extensions to get better key-chord interpretation when running in the terminal.
#+begin_src emacs-lisp
  (use-package kkp
    :ensure t
    :if (not (display-graphic-p))
    :config
    (global-kkp-mode +1))
#+end_src

* UI & Appearance
** Theme
*** Automatic Theme Switching
Switch between light and dark themes based on macOS system appearance.
#+begin_src emacs-lisp
  (use-package modus-themes
    :ensure t
    :config
    ;; Use Emacs tooltips for consistent styling (not GTK)
    (setq x-gtk-use-system-tooltips nil)
    (add-hook 'after-init-hook (lambda () (load-theme 'modus-operandi-tinted t))))
#+end_src

** Font Configuration
Set Berkeley Mono as the primary font family. Using default-frame-alist
is faster than set-face-attribute at startup.
#+begin_src emacs-lisp
  (add-to-list 'default-frame-alist '(font . "Berkeley Mono-16"))
  (set-face-attribute 'fixed-pitch nil :inherit 'default)
  (set-face-attribute 'default nil :font "Berkeley Mono-16")
  (set-face-attribute 'variable-pitch nil :inherit 'default)
#+end_src
** Frame Parameters
macOS-specific frame appearance settings are in early-init.el.
#+begin_src emacs-lisp :tangle no
  ;; Moved to early-init.el to prevent visual flash during startup before theme loads,
  ;; and for faster startup.
  ;; (add-to-list 'default-frame-alist '(ns-transparent-titlebar . t))
  ;; (add-to-list 'default-frame-alist '(ns-appearance . dark))
#+end_src

** Visual Bell
Use visual bell only, no audible bell.
#+begin_src emacs-lisp
  (setq ring-bell-function 'ignore)
  (setq visible-bell t)
#+end_src

** Basic UI Elements
UI elements are disabled in early-init.el for fastest startup.
#+begin_src emacs-lisp
  (setq inhibit-startup-screen t)
  (setq inhibit-startup-message t)
  (setq inhibit-startup-echo-area-message user-login-name)

  ;; Defer hl-line-mode to after init
  (add-hook 'after-init-hook #'global-hl-line-mode)
#+end_src

** Line Numbers
Enable line numbers in programming modes only.
#+begin_src emacs-lisp
  (add-hook 'prog-mode-hook #'display-line-numbers-mode)
  (setq display-line-numbers-type t)
#+end_src

** Word Wrapping
Enable visual line mode globally for better text wrapping.
#+begin_src emacs-lisp
  (setq word-wrap t)
  (add-hook 'after-init-hook #'global-visual-line-mode)
#+end_src

** Olivetti Mode
Center text for better readability (GUI only).
#+begin_src emacs-lisp
  (use-package olivetti
    :if (display-graphic-p)
    :commands olivetti-mode
    :custom
    (olivetti-body-width 86))
#+end_src

** ANSI Colors
Enable ANSI color support in compilation buffers.
#+begin_src emacs-lisp
  (use-package ansi-color
    :ensure nil
    :hook (compilation-filter . ansi-color-compilation-filter))
#+end_src

#+begin_src emacs-lisp
  (use-package multiple-cursors
    :bind
    (("C-S-c C-S-c" . mc/edit-lines)
     ("C->"         . mc/mark-next-like-this)
     ("C-<"         . mc/mark-previous-like-this)
     ("C-c C-<"     . mc/mark-all-like-this)
     ("C-\""        . mc/skip-to-next-like-this)
     ("C-:"         . mc/skip-to-previous-like-this)))
  (use-package iedit)
#+end_src
** Expand Region
Intelligently expand selection by semantic units.
#+begin_src emacs-lisp
  (use-package expand-region
    :bind ("C-=" . er/expand-region))
#+end_src

** Move Text
Move lines or regions up and down.
#+begin_src emacs-lisp
  (use-package move-text
    :bind
    (("M-n" . move-text-down)
     ("M-p" . move-text-up)))
#+end_src

** Zap to Char
Improved zap-to-char that doesn't delete the target character.
#+begin_src emacs-lisp
  (global-set-key [remap zap-to-char] 'zap-up-to-char)
#+end_src

** Paredit
Structural editing for Lisp code.
#+begin_src emacs-lisp
  (use-package paredit
    :hook
    ((emacs-lisp-mode . paredit-mode)
     (lisp-mode       . paredit-mode)
     (scheme-mode     . paredit-mode)
     (racket-mode     . paredit-mode)))

  ;; Built-in paren matching (deferred)
  (setq show-paren-delay 0)
  (add-hook 'after-init-hook #'show-paren-mode)
#+end_src

** Meow Mode
Modal editing with helix-style keybindings. Selection-first paradigm:
select text first, then act on it.
#+begin_src emacs-lisp
  (use-package meow
    :config
    ;; Define g prefix keymap for helix-style goto commands
    (define-prefix-command 'meow-goto-keymap)
    (define-key meow-goto-keymap (kbd "g") #'beginning-of-buffer)
    (define-key meow-goto-keymap (kbd "e") #'end-of-buffer)
    (define-key meow-goto-keymap (kbd "h") #'beginning-of-line)
    (define-key meow-goto-keymap (kbd "l") #'end-of-line)
    (define-key meow-goto-keymap (kbd "s") #'back-to-indentation)
    (define-key meow-goto-keymap (kbd "d") #'xref-find-definitions)
    (define-key meow-goto-keymap (kbd "r") #'xref-find-references)
    (define-key meow-goto-keymap (kbd "b") #'xref-go-back)

    (defun meow-setup-helix ()
      "Setup meow with helix-style keybindings."
      (setq meow-cheatsheet-layout meow-cheatsheet-layout-qwerty)

      ;; MOTION state (for special modes like dired, magit)
      (meow-motion-define-key
       '("j" . meow-next)
       '("k" . meow-prev)
       '("<escape>" . ignore))

      ;; NORMAL state - helix-style
      (meow-normal-define-key
       ;; Numbers for digit argument (allows 20j, 5w, etc.)
       '("0" . meow-digit-argument)
       '("1" . meow-digit-argument)
       '("2" . meow-digit-argument)
       '("3" . meow-digit-argument)
       '("4" . meow-digit-argument)
       '("5" . meow-digit-argument)
       '("6" . meow-digit-argument)
       '("7" . meow-digit-argument)
       '("8" . meow-digit-argument)
       '("9" . meow-digit-argument)
       '("-" . negative-argument)

       ;; Basic movement (hjkl)
       '("h" . meow-left)
       '("j" . meow-next)
       '("k" . meow-prev)
       '("l" . meow-right)

       ;; Word movement
       '("w" . meow-next-word)
       '("W" . meow-next-symbol)
       '("b" . meow-back-word)
       '("B" . meow-back-symbol)
       '("e" . meow-mark-word)

       ;; Find/Till (select to char)
       '("f" . meow-find)
       '("t" . meow-till)

       ;; Selection
       '("x" . meow-line)
       '("X" . meow-goto-line)
       '(";" . meow-reverse)
       '("," . meow-inner-of-thing)
       '("." . meow-bounds-of-thing)
       '("[" . meow-beginning-of-thing)
       '("]" . meow-end-of-thing)
       '("%" . mark-whole-buffer)

       ;; Editing
       '("d" . meow-kill)
       '("c" . meow-change)
       '("s" . meow-kill)
       '("y" . meow-save)
       '("p" . meow-yank)
       '("P" . meow-yank-pop)
       '("r" . meow-replace)
       '("R" . meow-swap-grab)
       '("u" . meow-undo)
       '("U" . undo-redo)
       '("J" . meow-join)
       '(">" . indent-rigidly-right-to-tab-stop)
       '("<" . indent-rigidly-left-to-tab-stop)

       ;; Insert mode entry
       '("i" . meow-insert)
       '("a" . meow-append)
       '("I" . meow-open-above)
       '("A" . meow-open-below)
       '("o" . meow-open-below)
       '("O" . meow-open-above)

       ;; Search
       '("/" . meow-visit)
       '("n" . meow-search)
       '("*" . meow-mark-symbol)

       ;; Misc
       '("q" . meow-quit)
       '("Q" . meow-goto-line)
       '("'" . repeat)
       '("z" . meow-pop-selection)
       '("Z" . meow-pop-all-selection)
       '("&" . meow-query-replace-regexp)
       '("<escape>" . meow-cancel-selection)

       ;; g prefix commands (helix-style)
       '("g" . meow-goto-keymap)))

    (meow-setup-helix)
    (meow-global-mode 1))
#+end_src

** Which Key
Display available keybindings in popup. Deferred to idle time for faster startup.
#+begin_src emacs-lisp
  (use-package which-key
    :defer 1
    :custom
    (which-key-idle-delay 0.3)
    (which-key-popup-type 'side-window)
    (which-key-side-window-location 'bottom)
    :config
    (which-key-mode))
#+end_src

** Window Navigation
*** Ace Window
Quick window switching with visual indicators.
#+begin_src emacs-lisp
  (use-package ace-window
    :bind ("M-o" . ace-window))
#+end_src
** Jump to Definition
*** Dumb Jump with xref
Fallback jump-to-definition using ripgrep.
#+begin_src emacs-lisp
  (use-package dumb-jump
    :defer t
    :commands dumb-jump-xref-activate
    :init
    ;; Defer xref setup to first prog-mode buffer
    (add-hook 'prog-mode-hook
              (lambda ()
                (add-hook 'xref-backend-functions #'dumb-jump-xref-activate nil t)))
    :custom
    (xref-search-program 'ripgrep))
#+end_src

* Completion & Search
** Vertico
Vertical completion UI for minibuffer.
#+begin_src emacs-lisp
  (use-package vertico
    :hook (after-init-hook . vertico-mode))

  (use-package vertico-posframe
    :after vertico
    :hook (vertico-mode . vertico-posframe-mode)
    :custom
    (vertico-posframe-truncate-lines t)
    :config

    (setq frame-resize-pixelwise t))
#+end_src

** Orderless
Flexible completion style with space-separated components.
Loads after vertico (which loads after-init).
#+begin_src emacs-lisp
  (use-package orderless
    :after vertico
    :custom
    (completion-styles '(orderless basic))
    (completion-category-overrides '((file (styles basic partial-completion))))
    (completion-category-defaults nil))
#+end_src

** Marginalia
Rich annotations for minibuffer completions.
#+begin_src emacs-lisp
  (use-package marginalia
    :hook (after-init-hook . marginalia-mode))
#+end_src
** Consult
#+begin_src emacs-lisp
  (use-package consult
    :demand t
    :bind (;; C-c bindings in `mode-specific-map'
           ("C-c M-x" . consult-mode-command)
           ("C-c h" . consult-history)
           ("C-c k" . consult-kmacro)
           ("C-c m" . consult-man)
           ("C-c i" . consult-info)
           ([remap Info-search] . consult-info)
           ;; C-x bindings in `ctl-x-map'
           ("C-x M-:" . consult-complex-command)     ;; orig. repeat-complex-command
           ("C-x b" . consult-buffer)                ;; orig. switch-to-buffer
           ("C-x 4 b" . consult-buffer-other-window) ;; orig. switch-to-buffer-other-window
           ("C-x 5 b" . consult-buffer-other-frame)  ;; orig. switch-to-buffer-other-frame
           ("C-x t b" . consult-buffer-other-tab)    ;; orig. switch-to-buffer-other-tab
           ("C-x r b" . consult-bookmark)            ;; orig. bookmark-jump
           ("C-x p b" . consult-project-buffer)      ;; orig. project-switch-to-buffer
           ;; Custom M-# bindings for fast register access
           ("M-#" . consult-register-load)
           ("M-'" . consult-register-store)          ;; orig. abbrev-prefix-mark (unrelated)
           ("C-M-#" . consult-register)
           ;; Other custom bindings
           ("M-y" . consult-yank-pop)                ;; orig. yank-pop
           ;; M-g bindings in `goto-map'
           ("M-g e" . consult-compile-error)
           ("M-g r" . consult-recent-file)
           ("M-g f" . consult-flymake)               ;; Alternative: consult-flycheck
           ("M-g g" . consult-goto-line)             ;; orig. goto-line
           ("M-g M-g" . consult-goto-line)           ;; orig. goto-line
           ("M-g o" . consult-outline)               ;; Alternative: consult-org-heading
           ("M-g m" . consult-mark)
           ("M-g k" . consult-global-mark)
           ("M-g i" . consult-imenu)
           ("M-g I" . consult-imenu-multi)
           ;; M-s bindings in `search-map'
           ("M-s d" . consult-fd)                  ;; Alternative: consult-fd
           ("M-s c" . consult-locate)
           ("M-s g" . consult-grep)
           ("M-s G" . consult-git-grep)
           ("M-s r" . consult-ripgrep)
           ("M-s l" . consult-line)
           ("M-s L" . consult-line-multi)
           ("M-s k" . consult-keep-lines)
           ("M-s u" . consult-focus-lines)
           ;; Isearch integration
           ("M-s e" . consult-isearch-history)
           :map isearch-mode-map
           ("M-e" . consult-isearch-history)         ;; orig. isearch-edit-string
           ("M-s e" . consult-isearch-history)       ;; orig. isearch-edit-string
           ("M-s l" . consult-line)                  ;; needed by consult-line to detect isearch
           ("M-s L" . consult-line-multi)            ;; needed by consult-line to detect isearch
           ;; Minibuffer history
           :map minibuffer-local-map
           ("M-s" . consult-history)                 ;; orig. next-matching-history-element
           ("M-r" . consult-history))                ;; orig. previous-matching-history-element

    ;; Enable automatic preview at point in the *Completions* buffer. This is
    ;; relevant when you use the default completion UI.
    :hook (completion-list-mode . consult-preview-at-point-mode)

    ;; The :init configuration is always executed (Not lazy)
    :init

    ;; Tweak the register preview for `consult-register-load',
    ;; `consult-register-store' and the built-in commands.  This improves the
    ;; register formatting, adds thin separator lines, register sorting and hides
    ;; the window mode line.
    (advice-add #'register-preview :override #'consult-register-window)
    (setq register-preview-delay 0.5)

    ;; Configure other variables and modes in the :config section,
    ;; after lazily loading the package.
    :config

    ;; Optionally configure preview. The default value
    ;; is 'any, such that any key triggers the preview.
    ;; (setq consult-preview-key 'any)
    ;; (setq consult-preview-key "M-.")
    ;; (setq consult-preview-key '("S-<down>" "S-<up>"))
    ;; For some commands and buffer sources it is useful to configure the
    ;; :preview-key on a per-command basis using the `consult-customize' macro.
    (consult-customize
     consult-theme :preview-key '(:debounce 0.2 any)
     consult-ripgrep consult-git-grep consult-grep consult-man
     consult-bookmark consult-recent-file
     consult-source-bookmark consult-source-file-register
     consult-source-recent-file consult-source-project-recent-file
     ;; :preview-key "M-."
     :preview-key '(:debounce 0.4 any))

    ;; Optionally configure the narrowing key.
    ;; Both < and C-+ work reasonably well.
    (setq consult-narrow-key "<") ;; "C-+"

    (with-eval-after-load 'project
      (require 'keymap) ;; keymap-substitute requires emacs version 29.1?
      (require 'cl-seq)

      (keymap-substitute project-prefix-map #'project-find-regexp #'consult-ripgrep)
      (cl-nsubstitute-if
       '(consult-ripgrep "Find regexp")
       (pcase-lambda (`(,cmd _)) (eq cmd #'project-find-regexp))
       project-switch-commands))


    ;; Optionally make narrowing help available in the minibuffer.
    ;; You may want to use `embark-prefix-help-command' or which-key instead.
    ;; (keymap-set consult-narrow-map (concat consult-narrow-key " ?") #'consult-narrow-help)

    ;; Use consult for xref with live preview
    (require 'consult-xref)
    (setq xref-show-xrefs-function #'consult-xref)
    (setq xref-show-definitions-function #'consult-xref))
#+end_src
** corfu
#+begin_src emacs-lisp
  (use-package corfu
    ;; TAB-and Go customizations
    :custom
    (corfu-cycle t)           ;; Enable cycling for `corfu-next/previous'
    (corfu-preselect 'prompt) ;; Always preselect the prompt
    ;; Enable auto completion, configure delay, trigger and quitting
    (corfu-auto t)
    (corfu-auto-prefix 1)
    (corfu-auto-delay 0.1)
    (corfu-auto-trigger ["." "," ">" ":"])

    ;; Use TAB for cycling, default is `corfu-complete'.
    :bind
    (:map corfu-map
          ("TAB" . corfu-next)
          ([tab] . corfu-next)
          ("S-TAB" . corfu-previous)
          ([backtab] . corfu-previous)
          ("C-TAB" . corfu-insert))
    :hook
    ((after-init . global-corfu-mode)
     (prog-mode . corfu-mode)
     (shell-mode . corfu-mode)
     (eshell-mode . corfu-mode)))

  (use-package corfu-popupinfo
    :ensure nil
    :after corfu
    :custom
    (corfu-popupinfo-delay '(0.5 . 0.2))
    (corfu-popupinfo-max-width 80)
    (corfu-popupinfo-max-height 20)
    (corfu-popupinfo-direction '(right left vertical))
    :hook ((global-corfu-mode . corfu-popupinfo-mode)
           (corfu-mode . corfu-popupinfo-mode)))
#+end_src
** cape
#+begin_src emacs-lisp
  (use-package cape
    :hook
    ((completion-at-point-functions . cape-dabbrev)
     (completion-at-point-functions . cape-file)
     (completion-at-point-functions . cape-elisp-block)))
#+end_src
* Programming
** Language Modes
*** C/C++
#+begin_src emacs-lisp
  (use-package simpc-mode
    :ensure nil
    :mode "\\.[hc]\\(pp\\)?\\'")
#+end_src

*** Nix
#+begin_src emacs-lisp
  (use-package nix-mode
    :defer t)

  (use-package nix-ts-mode
    :mode "\\.nix\\'")
#+end_src

*** Rust
#+begin_src emacs-lisp
  (use-package rust-mode
    :mode "\\.rs\\'"
    :custom
    (rust-mode-treesitter-derive t)
    (rust-format-on-save t)
    :hook
    ((rust-mode . (lambda () (setq indent-tabs-mode nil)))
     (rust-mode . prettify-symbols-mode)))
#+end_src

*** Go
#+begin_src emacs-lisp
  (use-package go-mode
    :mode "\\.go\\'"
    :hook (before-save . gofmt-before-save)
    :custom
    (gofmt-command "goimports"))
#+end_src

*** TypeScript
#+begin_src emacs-lisp
  (use-package typescript-mode
    :mode "\\.ts\\'"
    :hook (typescript-mode . hs-minor-mode))
#+end_src

*** Ruby
#+begin_src emacs-lisp
  (use-package ruby-mode
    :ensure nil
    :mode
    ("\\.\\(?:cap\\|gemspec\\|irbrc\\|gemrc\\|rake\\|rb\\|ru\\|thor\\)\\'"
     "\\(?:Brewfile\\|Capfile\\|Gemfile\\(?:\\.[a-zA-Z0-9._-]+\\)?\\|[rR]akefile\\)\\'"))
#+end_src

*** Zig
#+begin_src emacs-lisp
  (use-package zig-mode
    :defer t)

  (use-package zig-ts-mode
    :mode "\\.\\(zig\\|zon\\)\\'")
#+end_src

*** Fish Shell
#+begin_src emacs-lisp
  (use-package fish-mode
    :mode "\\.fish\\'")
#+end_src

*** Haskell
#+begin_src emacs-lisp
  (use-package haskell-mode
    :mode ("\\.hs\\'" "\\.hsc\\'" "\\.lhs\\'")
    :config
    ;; Ensure all haskell-mode components are loaded
    (require 'haskell nil t)
    (require 'haskell-completions nil t))
#+end_src

*** Racket
#+begin_src emacs-lisp
  (use-package racket-mode
    :mode "\\.rkt\\'")

  (use-package scribble-mode
    :defer t)
#+end_src

*** Lean
#+begin_src emacs-lisp
  (use-package nael
    :ensure nil
    :config (add-hook 'nael-mode-hook (lambda () (set-input-method "TeX")))
    :mode ("\\.lean\\'" . nael-mode))
  ;; Set up ligatures
  (use-package ligature
    :hook (nael-mode . ligature-mode)
    :hook (haskell-mode . ligature-mode)
    :config
    ;; Enable the "www" ligature in every possible major mode
    (ligature-set-ligatures 't '("www"))
    ;; Enable traditional ligature support in eww-mode, if the
    ;; `variable-pitch' face supports it
    (ligature-set-ligatures 'eww-mode '("ff" "fi" "ffi"))
    ;; Enable all Cascadia Code ligatures in programming modes
    (ligature-set-ligatures 'prog-mode '("|||>" "<|||" "<==>" "<!--" "####" "~~>" "***" "||=" "||>"
                                         ":::" "::=" "=:=" "===" "==>" "=!=" "=>>" "=<<" "=/=" "!=="
                                         "!!." ">=>" ">>=" ">>>" ">>-" ">->" "->>" "-->" "---" "-<<"
                                         "<~~" "<~>" "<*>" "<||" "<|>" "<$>" "<==" "<=>" "<=<" "<->"
                                         "<--" "<-<" "<<=" "<<-" "<<<" "<+>" "</>" "###" "#_(" "..<"
                                         "..." "+++" "/==" "///" "_|_" "www" "&&" "^=" "~~" "~@" "~="
                                         "~>" "~-" "**" "*>" "*/" "||" "|}" "|]" "|=" "|>" "|-" "{|"
                                         "[|" "]#" "::" ":=" ":>" ":<" "$>" "==" "=>" "!=" "!!" ">:"
                                         ">=" ">>" ">-" "-~" "-|" "->" "--" "-<" "<~" "<*" "<|" "<:"
                                         "<$" "<=" "<>" "<-" "<<" "<+" "</" "#{" "#[" "#:" "#=" "#!"
                                         "##" "#(" "#?" "#_" "%%" ".=" ".-" ".." ".?" "+>" "++" "?:"
                                         "?=" "?." "??" ";;" "/*" "/=" "/>" "//" "__" "~~" "(*" "*)"
                                         "\\\\" "://")))
#+end_src

*** Terraform
#+begin_src emacs-lisp
  (use-package terraform-mode
    :defer t
    :mode ("\\.tf\\'" "\\.tfvars\\'")
    :custom
    (terraform-format-on-save t)
    :hook (terraform-mode . outline-minor-mode))
#+end_src

*** Dockerfile
#+begin_src emacs-lisp
  (use-package dockerfile-mode
    :defer t)
#+end_src

*** Helm Charts
#+begin_src emacs-lisp
  (use-package poly-helm-mode
    :ensure nil)
#+end_src

*** Groovy
#+begin_src emacs-lisp
  (use-package groovy-mode
    :defer t)
#+end_src
*** OCaml
#+begin_src emacs-lisp
  (use-package tuareg
    :mode ("\\.ml\\'" "\\.mli\\'"))
#+end_src

** LSP with Eglot
Built-in LSP client for language intelligence.
#+begin_src emacs-lisp
  (use-package eglot
    :ensure nil
    :hook ((nix-ts-mode . eglot-ensure)
           (rust-ts-mode . eglot-ensure)
           (rust-mode . eglot-ensure)
           (go-ts-mode . eglot-ensure)
           (go-mode . eglot-ensure)
           (typescript-ts-mode . eglot-ensure)
           (typescript-mode . eglot-ensure)
           (js-ts-mode . eglot-ensure)
           (ruby-ts-mode . eglot-ensure)
           (ruby-mode . eglot-ensure)
           (zig-ts-mode . eglot-ensure)
           (haskell-mode . eglot-ensure)
           (tuareg-mode . eglot-ensure)
           (terraform-mode . eglot-ensure)
           (c-ts-mode . eglot-ensure)
           (c++-ts-mode . eglot-ensure)
           (dockerfile-ts-mode . eglot-ensure)
           (nael-mode . eglot-ensure)
           (racket-mode . eglot-ensure))
    :custom
    (eglot-autoshutdown t)
    (eglot-send-changes-idle-time 0.1)
    (eglot-extend-to-xref t)
    :config
    (fset #'jsonrpc--log-event #'ignore)
    (add-to-list 'eglot-server-programs '(nix-ts-mode . ("nixd")))
    (add-to-list 'eglot-server-programs '(nael-mode . ("lake" "serve")))
    (add-to-list 'eglot-server-programs '(racket-mode . ("racket-langserver"))))
#+end_src

** Eldoc Box
Display hover documentation in a styled child frame. Zed-like appearance.
#+begin_src emacs-lisp
  (use-package eldoc-box
    :hook (eglot-managed-mode . eldoc-box-hover-mode)
    :custom
    (eldoc-box-max-pixel-width 600)
    (eldoc-box-max-pixel-height 400)
    (eldoc-box-cleanup-interval 1)
    (eldoc-box-clear-with-C-g t)
    :config
    (setq eldoc-box-frame-parameters
          '((left-fringe . 8)
            (right-fringe . 8)
            (internal-border-width . 1)
            (vertical-scroll-bars . nil)
            (horizontal-scroll-bars . nil)
            (menu-bar-lines . 0)
            (tool-bar-lines . 0)
            (line-spacing . 0.1)
            (no-special-glyphs . t)
            (no-accept-focus . t)
            (no-focus-on-map . t))))
#+end_src

* Project & Version Control
** Git Integration
*** Magit
Powerful Git porcelain for Emacs.
#+begin_src emacs-lisp
  (use-package magit
    :bind ("C-x g" . magit-status)
    :commands magit-file-dispatch
    :custom
    (magit-refresh-status-buffer nil)
    (magit-blame-styles
     '((headings
        (heading-format . "%-20a %C %s\n"))
       (highlight
        (highlight-face . magit-blame-highlight))
       (lines
        (show-lines . t)
        (show-message . t)))))
#+end_src

*** Forge
GitHub/GitLab integration for Magit.
#+begin_src emacs-lisp
  (use-package forge
    :after magit)
#+end_src

*** GitHub CLI Auth Source
Use gh CLI for authentication with Forge. Deferred until forge loads.
#+begin_src emacs-lisp
  (with-eval-after-load 'forge
    (cl-defun auth-source-ghcli-search (&rest spec
                                              &key backend require
                                              type max host user port
                                              &allow-other-keys)
      "Given a property list SPEC, return search matches from the `:backend'.
    See `auth-source-search' for details on SPEC."
      (cl-assert (or (null type) (eq type (oref backend type)))
                 t "Invalid GH CLI search: %s %s")

      (when-let* ((hostname (string-remove-prefix "api." host))
                  (ghub_ident (split-string user "\\^"))
                  (username (car ghub_ident))
                  (package (cadr ghub_ident))
                  (cmd (format "gh auth token --hostname '%s'" hostname))
                  (token (when (string= package "forge")
                           (string-trim-right (shell-command-to-string cmd))))
                  (retval (list
                           :host hostname
                           :user username
                           :secret token)))
        (auth-source-do-debug
         "auth-source-ghcli: return %s as final result (plus hidden password)"
         (seq-subseq retval 0 -2))
        (list retval)))

    (defvar auth-source-ghcli-backend
      (auth-source-backend
       :source "."
       :type 'gh-cli
       :search-function #'auth-source-ghcli-search)
      "Auth-source backend for GH CLI.")

    (defun auth-source-ghcli-backend-parse (entry)
      "Create a GH CLI auth-source backend from ENTRY."
      (when (eq entry 'gh-cli)
        (auth-source-backend-parse-parameters entry auth-source-ghcli-backend)))

    (if (boundp 'auth-source-backend-parser-functions)
        (add-hook 'auth-source-backend-parser-functions
                  #'auth-source-ghcli-backend-parse)
      (advice-add 'auth-source-backend-parse :before-until
                  #'auth-source-ghcli-backend-parse))

    (setq auth-sources '(gh-cli)))
#+end_src

*** Jujutsu VCS
Support for jj version control system.
#+begin_src emacs-lisp
  (use-package vc-jj
    :defer t)

  (use-package jj-mode
    :ensure nil
    :defer t
    :config
    (setq vc-handled-backends (delq 'JJ vc-handled-backends)))
#+end_src

*** Diff HL
Visual indication of git changes in the fringe. Faster than git-gutter.
#+begin_src emacs-lisp
  (use-package diff-hl
    :hook (after-init . global-diff-hl-mode)
    :config
    ;; Dired integration
    (with-eval-after-load 'dired
      (require 'diff-hl-dired nil t)
      (when (fboundp 'diff-hl-dired-mode)
        (add-hook 'dired-mode-hook #'diff-hl-dired-mode)))
    ;; Magit integration - functions defined in main file, just add hooks
    (with-eval-after-load 'magit
      (add-hook 'magit-pre-refresh-hook #'diff-hl-magit-pre-refresh)
      (add-hook 'magit-post-refresh-hook #'diff-hl-magit-post-refresh)))
#+end_src

* Applications & Utilities
** Org Mode
*** Core Org Configuration
#+begin_src emacs-lisp
  (use-package org
    :ensure nil
    :bind ("C-c a" . org-agenda)
    :custom
    (org-agenda-files (list "~/src/femiagbabiaka/org/agenda"))
    ;; Edit settings
    (org-auto-align-tags nil)
    (org-tags-column 0)
    (org-catch-invisible-edits 'show-and-error)
    (org-special-ctrl-a/e t)
    (org-insert-heading-respect-content t)
    ;; Styling
    (org-hide-emphasis-markers t)
    (org-pretty-entities t)
    (org-agenda-tags-column 0)
    (org-ellipsis "â€¦"))
#+end_src

*** Org Modern
Modern styling for org-mode.
#+begin_src emacs-lisp
  (use-package org-modern
    :hook (org-mode . org-modern-mode))
#+end_src

*** Org Roam
Zettelkasten note-taking system.
#+begin_src emacs-lisp
  (use-package org-roam
    :commands
    (org-roam-buffer-toggle
     org-roam-node-find
     org-roam-graph
     org-roam-node-insert
     org-roam-capture
     org-roam-dailies-capture-today)
    :bind
    (("C-c r l" . org-roam-buffer-toggle)
     ("C-c r f" . org-roam-node-find)
     ("C-c r g" . org-roam-graph)
     ("C-c r i" . org-roam-node-insert)
     ("C-c r c" . org-roam-capture)
     ("C-c r j" . org-roam-dailies-capture-today))
    :custom
    (org-roam-directory (file-truename "~/src/femiagbabiaka/org/notes"))
    (org-roam-node-display-template
     (concat "${title:*} " (propertize "${tags:10}" 'face 'org-tag)))
    :config
    (org-roam-db-autosync-mode)
    (require 'org-roam-protocol))
#+end_src

** RSS Feed Reader
*** Elfeed
#+begin_src emacs-lisp
  (use-package elfeed
    :bind ("C-x w" . elfeed)
    :custom
    (elfeed-feeds
     '(("https://www.computerenhance.com/feed" programming)
       ("https://jyn.dev/atom.xml" programming)
       ("https://ansuz.sooke.bc.ca/rss" programming)
       ("https://andreyor.st/feed.xml" programming))))
#+end_src

*** Elfeed Tube
YouTube feed support for Elfeed.
#+begin_src emacs-lisp
  (use-package elfeed-tube
    :after elfeed
    :bind
    (:map elfeed-show-mode-map
          ("F" . elfeed-tube-fetch)
          ([remap save-buffer] . elfeed-tube-save)
          :map elfeed-search-mode-map
          ("F" . elfeed-tube-fetch)
          ([remap save-buffer] . elfeed-tube-save))
    :config
    (elfeed-tube-setup)
    (elfeed-tube-add-feeds
     '("veritasium"
       "netdevconf"
       "XahLee"
       "BillyEllis"
       "protesilaos"
       "TsodingDaily"
       "ALifeEngineered")))
#+end_src

** Terminal Emulator
#+begin_src emacs-lisp
  (use-package vterm
    :commands vterm)
#+end_src

** EPUB Reader
#+begin_src emacs-lisp
  (use-package nov
    :mode ("\\.epub\\'" . nov-mode)
    :hook
    (nov-mode . (lambda ()
                  (face-remap-add-relative 'shr-h1 :height 1.8 :weight 'bold)
                  (face-remap-add-relative 'shr-h2 :height 1.6 :weight 'bold)
                  (face-remap-add-relative 'shr-h3 :height 1.4 :weight 'bold)
                  (face-remap-add-relative 'shr-text :height 1.3)
                  (face-remap-add-relative 'shr-code :height 1.2)
                  (display-line-numbers-mode -1))))
#+end_src

* AI Assistant
** gptel
LLM interface for Emacs with streaming support and multiple backends.
#+begin_src emacs-lisp
  (use-package gptel
    :commands (gptel gptel-send gptel-rewrite gptel-menu)
    :bind (("C-c g g" . gptel)
           ("C-c g s" . gptel-send)
           ("C-c g r" . gptel-rewrite)
           ("C-c g m" . gptel-menu))
    :config
    (setq gptel-default-mode 'org-mode)
    (require 'gptel-anthropic)
    (require 'gptel-transient)
    (setq gptel-model 'claude-sonnet-4-20250514)
    (setq gptel-backend
          (gptel-make-anthropic "Claude"
            :stream t
            :key (lambda ()
                   (auth-source-pick-first-password
                    :host "api.anthropic.com"
                    :user "apikey")))))
#+end_src

** gptel-aibo
Invoke LLM at cursor with inline diff preview. Zed-like inline editing.
#+begin_src emacs-lisp
  (use-package gptel-aibo
    :after gptel
    :bind (("C-c g i" . gptel-aibo-summon))
    :custom
    (gptel-aibo-auto-apply nil))
#+end_src

** gptel-emacs-tools
Custom tools to allow LLMs to inspect Emacs state, buffers, and projects.
Provides claude-code-ide style capabilities for gptel.
#+begin_src emacs-lisp
  (use-package gptel-emacs-tools
    :ensure nil
    :after gptel
    :load-path "~/.emacs.d/lisp"
    :config
    (setq gptel-tools gptel-emacs-tools-all))
#+end_src
